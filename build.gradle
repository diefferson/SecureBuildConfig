import java.text.SimpleDateFormat

plugins {
    id 'groovy'
    id 'maven'
    id "com.gradle.plugin-publish" version "0.9.7"
}

sourceCompatibility = '1.7'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

if (!hasProperty('mainClass')) {
    ext.mainClass = ''
}

group = 'br.com.diefferson'
version = '1.0.7'

repositories {
    mavenCentral()
}

configurations {
    provided
    compile.extendsFrom provided
}

task createClasspathManifest {
    def outputDir = file("${buildDir}/${name}")

    inputs.files sourceSets.main.runtimeClasspath
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        file("${outputDir}/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join ("\n")
    }
}


dependencies {
    provided gradleApi()
    testCompile group: 'junit', name: 'junit', version: '4.10'
    testCompile ('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude module : 'groovy-all'
    }
    testCompile gradleTestKit()
    testRuntime files(createClasspathManifest)
}


jar {
    dependsOn configurations.runtime
    from {
        (configurations.runtime - configurations.provided).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}


task copyBintrayTemplate (type: Copy) {
    from "${projectDir}/templates"
    into "${project.buildDir}"
    include 'bintray.json.template'
    rename { file -> 'bintray.json' }
    expand (version: version, releasedate: releaseDate (), mavenRepositoryPath: mavenRepositoryPath ())
}

task copyPOMTemplate (type: Copy) {
    from "${projectDir}/templates"
    into "${buildDir}/libs"
    include 'pom.xml.template'
    rename { file -> "${project.name}-${project.version}.pom" }
    expand (version: version)
}

assemble.dependsOn copyBintrayTemplate
assemble.dependsOn copyPOMTemplate


pluginBundle {
    // These settings are set for the whole plugin bundle
    website = 'https://github.com/diefferson/SecureBuildConfig'
    vcsUrl = 'https://github.com/diefferson/SecureBuildConfig'

    plugins {

        // another plugin
        secureBuildConfig {
            id = 'br.com.diefferson.securebuildconfig'
            displayName = 'Secure Build config plugin for Android Gradle projects'
            description = 'A build config is a generated class holding constants set by the build script. This plugin provides a secure interface to store your project constants.'
            tags = ['android', 'buildconfig', 'secure', 'constants', 'generated', 'kotlin']
            version = project.version
        }
    }
}


task wrapper(type: Wrapper) {
    gradleVersion = '3.3'
}

test {
    testLogging {
        events "failed"
        exceptionFormat "full"
        showStandardStreams = true
    }
}

def mavenRepositoryPath ()
{
    "gradle/plugin/br/com/diefferson/SecureBuildConfig/${version}/"
}

def releaseDate ()
{
    new SimpleDateFormat("yyyy-MM-dd").format (new Date())
}